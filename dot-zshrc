# Minimal, fast defaults
export ZDOTDIR="$HOME"
export HISTFILE="$HOME/.zsh_history"
export SAVEHIST=5000
export HISTSIZE=5000

# Core options early (before plugins, so they apply)
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_FIND_NO_DUPS
setopt SHARE_HISTORY
setopt AUTO_CD
setopt CORRECT
setopt CORRECT_ALL  # Standard: Suggests corrections for entire command line (e.g., multi-word)
setopt EXTENDED_GLOB  # Standard: Enables advanced glob patterns (e.g., git rm (*~cache|*.log))

# Zimfw outside home (persisted volume)
export ZIM_HOME=/opt/zim
export ZIM_CONFIG_FILE=/opt/zim/.zimrc

# Ensure zimfw bootstrap exists (safe no-op once baked in image)
if [[ ! -e ${ZIM_HOME}/zimfw.zsh ]]; then
  curl -fsSL --create-dirs -o ${ZIM_HOME}/zimfw.zsh \
    https://github.com/zimfw/zimfw/releases/latest/download/zimfw.zsh
fi

# Rebuild init if .zimrc newer than init.zsh (idempotent)
if [[ ! ${ZIM_HOME}/init.zsh -nt ${ZIM_CONFIG_FILE} ]]; then
  source ${ZIM_HOME}/zimfw.zsh init -q
fi

# Initialize Zim modules (autosuggestions, syntax-highlighting, history-substring, git, fzf-tab, etc.)
source ${ZIM_HOME}/init.zsh

# History substring search on arrow keys (terminfo-safe)
zmodload zsh/terminfo
bindkey "${terminfo[kcuu1]}" history-substring-search-up
bindkey "${terminfo[kcud1]}" history-substring-search-down

# fzf defaults + key bindings (Ubuntu package installs examples)
export FZF_DEFAULT_OPTS='--height 40% --layout=reverse --border'
if [[ -r /usr/share/doc/fzf/examples/key-bindings.zsh ]]; then
  source /usr/share/doc/fzf/examples/key-bindings.zsh
fi
if [[ -r /usr/share/doc/fzf/examples/completion.zsh ]]; then
  source /usr/share/doc/fzf/examples/completion.zsh
fi

# fzf-tab niceties
zmodload zsh/complist
zstyle ':completion:*' menu select
zstyle ':fzf-tab:*' switch-group ',' '.'

# Prompt
# Starship prompt (guard if missing)
if command -v starship >/dev/null 2>&1; then
  export STARSHIP_CONFIG="$HOME/.config/starship.toml"
  eval "$(starship init zsh)"
fi

# Set default directory to app folder (if not already there)
if [ "$PWD" != "$HOME/app" ]; then
    mkdir -p "$HOME/app"
    cd "$HOME/app"
fi

# asdf env for non-login zsh
[[ -f /etc/profile.d/asdf.sh ]] && . /etc/profile.d/asdf.sh

# tmux stuff
ta() {
  command -v tmux >/dev/null 2>&1 || { echo "tmux not found"; return 1; }
  tmux start-server >/dev/null 2>&1 || true
  tmux has-session -t dev 2>/dev/null || tmux new-session -d -s dev -n shell "/usr/bin/zsh -l"
  tmux attach -t dev
}

tr() {
  if ! command -v tmux >/dev/null 2>&1; then
    echo "tmux not found"; return 1
  fi

  # If inside tmux and in session 'dev', swap seamlessly server-side.
  if [[ -n "$TMUX" ]] && [[ "$(tmux display-message -p '#S' 2>/dev/null)" == "dev" ]]; then
    tmux new-session -d -s dev.new "/usr/bin/zsh -l" \; \
         switch-client -t dev.new \; \
         kill-session -t dev \; \
         rename-session -t dev.new dev
    return
  fi

  # Outside tmux (or in another session): kill, recreate, and attach.
  tmux kill-session -t dev 2>/dev/null || true
  tmux new-session -d -s dev "/usr/bin/zsh -l"
  tmux attach -t dev
}

tsa() {
  command -v tmux >/dev/null 2>&1 || { echo "tmux not found"; return 1; }
  tmux start-server >/dev/null 2>&1 || true
  tmux has-session -t dev-server 2>/dev/null || tmux new-session -d -s dev-server -n shell "/usr/bin/zsh -l"
  tmux attach -t dev-server
}

tsr() {
  if ! command -v tmux >/dev/null 2>&1; then
    echo "tmux not found"; return 1
  fi

  # If inside tmux and in session 'dev-server', swap seamlessly server-side.
  if [[ -n "$TMUX" ]] && [[ "$(tmux display-message -p '#S' 2>/dev/null)" == "dev-server" ]]; then
    tmux new-session -d -s dev-server.new "/usr/bin/zsh -l" \; \
         switch-client -t dev-server.new \; \
         kill-session -t dev-server \; \
         rename-session -t dev-server.new dev-server
    return
  fi

  # Outside tmux (or in another session): kill, recreate, and attach.
  tmux kill-session -t dev-server 2>/dev/null || true
  PROJECT_DIR=$HOME/app
  tmux new-session -d -s dev-server -n server -c "$PROJECT_DIR" "./dev-server.sh run"
}

# For UV Python
export PATH="$HOME/.local/bin:$PATH"

# Common aliases
alias ll='ls -alF'
alias la='ls -A'
alias l='ls -CF'

# Git aliases
alias gs='git status'
alias ga='git add'
alias gc='git commit'
alias gp='git push'
alias gl='git log --oneline'
alias gd='git diff'
alias lg='git lg'
alias gundo='git reset --soft HEAD~1'
alias gst='git status'  # OMZ-style shortcut

# Phoenix development aliases
alias phx="mix phx.server"
alias phxd="MIX_ENV=dev mix phx.server"
alias phxt="MIX_ENV=test mix test"
alias ecto="mix ecto"
alias iex="iex -S mix"

# Database shortcuts
alias db.create="mix ecto.create"
alias db.migrate="mix ecto.migrate"
alias db.reset="mix ecto.reset"
alias db.seed="mix ecto.seed"

# Other Aliases
alias cld="claude --dangerously-skip-permissions"